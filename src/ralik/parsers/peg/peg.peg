Grammar    <- Spacing Definition+ EndOfFile
Definition <- Identifier '<-' Expression
Expression <- Sequence ('/' Sequence)*
Sequence   <- Prefix*
Prefix     <- ('&' / '!')? Suffix
Suffix     <- Primary [?*+]?
Primary    <- Identifier !'<-'
            / '(' Expression ')'
            / Literal
	    / Class
	    / '.'
Identifier <- [a-zA-Z_][a-zA-Z0-9_]*
Literal    <- ['] (!['] Char)* ['] Spacing
            / ["] (!["] Char)* ["] Spacing
Class      <- '[' (!']' Range)* ']' Spacing
Range      <- Char '-' !']' Char / Char
Char       <- '\\' [nrtf'\"]
            / '\\' ']'
            / '\\' '['
            / '\\' '\\'
            / '\\' [0-2] [0-7] [0-7]
            / '\\' [0-7] [0-7]?
            / !'\\' .
Spacing    <- (Space / Comment)*
Comment    <- '#' (!EndOfLine . )* EndOfLine
Space      <- ' ' / EndOfLine / [\t\f]
EndOfLine  <- '\r\n' / '\n' / '\r'
EndOfFile  <- !.
